;Brosig BIOS-2RF 2.40 PN BIOS-256K-RF
;****************************************
;CPM-BIOS FUER Z1013 UND 2*MP-RAM-FLOPPY
;IG-HC 12/88
;VERS. 2.40
;(c) by Rainer Brosig
;****************************************
;Anpassung an BWS-Platine von H.Poppe
;ähnlich AC1 BWS 2k COLOR/MONOCHROM
;Adresse E800h (Ueberdeckung orig. BWS!)
;Version 2.40BWS01 Aufloesung 64x32
;Syntax an M80MS / L80MS angepasst
;Matthias H./MHC "Lötspitze" 06.03.2014
;****************************************
;
AKTLW	EQU	00004H
SOIL	EQU	00016H	;START OF INPUT LINE
ARG1	EQU	0001BH
ARG2	EQU	0001DH
KUZI	EQU	0001FH
ARG3	EQU	00023H
CURSR	EQU	0002BH
CCP	EQU	0C800H
BOS	EQU	0D006H
BWSA	EQU	0E800H	;ANFANG BWS-Karte
BWSE	EQU	0EFFFH	;ENDE BWS-KARTE
STBIO	EQU	0DE00H	;BIOS Startadresse
MON	EQU	0F000H	;MONITOR-RET
BEEP	EQU	0FFDCH
DRAKK	EQU	0FFE8H
NDISK	EQU	2	;ZWEI LW
STAT	EQU	0FFF7H
GADDA	EQU	098H
GADDB	EQU	058H
CZSP	EQU	1FH	;CURSOR-ZWISCHEN-SP.
ZILAD	EQU	25H	;KOPFINHALT LOAD
SPALT	EQU	64	;BS-SPALTENZAHL
LCS	EQU	6	;LAENGE CHECKSUMMENBEREICH
CS	EQU	0F2H	;KONTROLL-CHECKSUMME
ZL	EQU	SPALT	;ZEILENLAENGE
BSTAB	EQU	47H
PIOD	EQU	34H	;ADR.V.24 E/A-MOD.
PIOC	EQU	35H
MLZT	EQU	4	;MERKE LETZTES ZEICHEN TAST.
TYPP	EQU	PUANF-4H	; TYPKENNZEICHENZELLE PUFFER
ARG1P	EQU	PUANF-10H	; ARG1 PUFFER
ARG2P	EQU	PUANF-0EH	; ARG2 PUFFER
ARG3P	EQU	PUANF-0CH	; STARTADRESSE IM PUFFER
PUANF	EQU	0F0H	; PUFFERANFANG NAME
DATA	EQU	13H	; KOPFADR. DES LETZTEN BLOCKS

;
	ASEG
	.PHASE STBIO
;
BIOS:	JP	BOOT
WBOTE:	JP	WBOOT
	JP	CONST
	JP	CONIN
	JP	COOUT
	JP	LIST
	JP	PUNCH
	JP	READR
	JP	HOME
	JP	SDISK
	JP	STRCK
	JP	SSEC
	JP	SDMA
	JP	READ
	JP	WRITE
	JP	LSTS
	JP	STRAN
	RET
	NOP
	NOP
	RET
	NOP
	NOP
EXDR:	JP	DRAKK	;EXTERNER DRUCKERTREIBER
MGADA:	DB	98H
MGADB:	DB	58H
EXDT:	DB	0	;SCHALTER, EXT. DR.TR. 0=INTERN
F2_4:	DB	0	;SCHALTER TAKT 0=AUTOANPASSUNG
			;              2=2MHz, 4=4MHz
;
;*********************************************************
;
;KOPF AUF SPUR NULL SETZEN
;
HOME:	LD	BC,0	;SPURNUMMER=0 
	JR	STRCK
;
;LAUFWERK AUSWAEHLEN
;INP:C=LW-NUMMER
;OUT:HL=DPH VEKTOR
;
SDISK:	LD	HL,0	;FEHLERET CODE
	PUSH	BC
	LD	B,0
	LD	A,C	;C=LW-NUMMER
	CP	NDISK	;ANZ. LW -1
	JR	NC,NVORH
DISKS:	LD	(AKTLW),A
	OR	A	;LW A?
	JR	NZ,LWB	;NICHT A
	LD	A,(MGADA)
	LD	HL,DPBA
	JR	LWEIN
LWB:	LD	A,(MGADB)
	LD	HL,DPBB
LWEIN:	LD	(GADDR),A
NVORH:	POP	BC
	RET
;
;SECTORTRANSFORMATION
;INP:BC=LOG.SECTOR
;    DE=ADR.TABELLE
;
STRAN:	LD	B,0
	LD	A,D
	OR 	A
	JR	NZ,STRN1
	LD	H,B
	LD	L,C
	INC	HL
	RET
;
STRN1:	EX	DE,HL
	ADD	HL,BC
	LD	A,(HL)	;NEUE NUMMER
	LD	(SEC1),A
	LD	L,A
	RET
;
;SEKTOR EINSTELLEN
;INP:C=SEC.NUMMER
;OUT:(SEC1)=C
;
SSEC:	LD	HL,SEC1
	LD	(HL),C
	INC	HL
	LD	(HL),0
	RET
;
;SPURWAHL
;INP:C=SPURNUMMER
;OUT:(TRACK)=C
;
STRCK:	LD	HL,TRACK
	LD	(HL),C
	INC	HL
	LD	(HL),0
	RET
;
;DMA SETZEN
;INP: BC=DMA
;
SDMA:	LD	(DMA),BC
	RET
;
;KALTSTART
;
MT1:	DB	0CH,0DH,0AH,0AH
	DB	' CP/M 2.2 Z1013 IG-HC',0DH,0AH,0AH
	DB	' mit 2*256K RAM-Floppy V. 2.4',0DH,0AH,0AH,0AH
	DB	'RAM-Floppy loeschen? (K)',0DH,0AH
	DB	'RAM-Floppy laden?    (L) :',0
MT2:	DB	0DH,0AH,'A oder B ? :',0 
MT3:	DB	0DH,0AH,0AH,'weiter mit CP/M? (N)',0
MT4:	DB	0DH,0AH,0AH,'RAM-Floppy laden? (Y)',0
MT5:	DB	0DH,0AH,0AH,'RAM-Floppy saven? (Y)',0
MT6:	DB	0DH,0AH,0AH
	DB	'CCP zerstoert, muss von Kassette'
	DB	' nachgeladen werden!',0
OUT$:	LD	A,(DE)
	OR	A
	RET	Z	
	LD	C,A
	CALL	COOUT
	INC	DE
	JR	OUT$
;
BOOT:	LD	SP,80H
	IN	A,(4)
	SET	7,A
	OUT	(4),A	;64*16	
	XOR	A
	LD	(AKTLW),A
	LD	A,8
	LD	(TAPUF),A
	LD	DE,MT1
	CALL	OUT$	
BO1:	CALL	CONIN
	CP	0DH
	JP	Z,KSTRT	
	CP	3
	JP	Z,MON
	RES	5,A
	CP	'K'
	JR	Z,BO2
	CP	'L'
	JR	NZ,BO1
BO2:	LD	(MKOM),A
	LD	C,A
	CALL	COOUT
	LD	DE,MT2
	CALL	OUT$
BO3:	CALL	CONIN
	RES	5,A
	LD	C,A
	CP	3
	JR	Z,KSTRT
	SUB	41H
	JR	C,BO3
	CALL	COOUT
	LD	C,A
	CALL	SDISK
	LD	A,(MKOM)
	CP	'K'
	CALL	Z,RFDEL	;RF-DELETE
	CP	'L'
	CALL	Z,LDDSK
KSTRT:	CALL	INIT
	LD	A,8
	LD	(TAPUF),A
	XOR	A
	LD	(TRACK),A
	LD	(SEC1),A
	LD	SP,80H
	JP	GOCPM
;
WBOOT:	LD	DE,MT3
	CALL	OUT$
	LD	A,8
	LD	(TAPUF),A	;LOESCHEN ZEICHEN
WBO1:	CALL	CONIN
	RES	5,A	;GROSS
	CP	'N'
	JR	Z,SAV1
	LD	DE,MT4
	CALL	OUT$
	CALL	CONIN
	RES	5,A
	CP	'Y'
	CALL	Z,LDDSK
	JR	GOCPM
SAV1:	LD	DE,MT5
	CALL	OUT$
	CALL	CONIN
	RES	5,A
	CP	'Y'
	CALL	Z,SAVED
	CALL	CHIT
	IN	A,(4)
	RES	7,A
	OUT	(4),A
	JP	MON
;
GOCPM:	LD	HL,CCP
	XOR	A
	LD	(CCP+7),A	;PUFFER LOESCHEN
	LD	B,LCS	;LAENGE CS-BEREICH
GCPM1:	ADD	A,(HL)
	INC	HL
	DJNZ	GCPM1
	CP	CS
	JR	Z,GCPM2
	LD	DE,MT6
	CALL	OUT$
	RST	38H		;KALTSTART
GCPM2:	LD	A,8
	LD	(TAPUF),A
	LD	BC,80H
	CALL	SDMA
	LD	A,(AKTLW)
	CP	NDISK
	JR	C,OK
	XOR	A
OK:	LD	C,A
	JP	CCP
;
;ZEICHEN VON TASTATUR <A>
;
CONIN:	CALL	CHIT
	RST	20H
	DB	1
	PUSH	AF
	CALL	CHTI
	POP	AF
	RET
;
;CHANGE AKTLWE/TAPUF
;
CHIT:	LD	A,(AKTLW)
	LD	(IOBPF),A
	LD	A,(TAPUF)
CHRET:	LD	(AKTLW),A
	RET
;
CHTI:	LD	A,(AKTLW)
	LD	(TAPUF),A
	LD	A,(IOBPF)
	JR	CHRET
;
;ZEICHEN AUF BILDSCHIRM <C>
;
COOUT:	PUSH	AF
	PUSH	HL
	PUSH	BC
	PUSH	DE
	CALL	ZAG
	POP	DE
	POP	BC
	POP	HL
	POP	AF
	RET
;
ZAG:	LD	A,(MLZBS)	;MERKE LETZTES ZEICHEN
	CP	1BH
	JP	Z,ANKXY
	LD	A,1BH
	CP	C
	JP	Z,MESC
	LD	A,C
	OR	A
	RET	Z
	CP	88H
	JR	C,ZAG5
	AND	7FH
ZAG5:	LD	HL,KTAB
	LD	BC,LKTAB-KTAB	;Laenge
	CPIR		;SUCHEN NACH ST.-ZEICHEN
	JR	NZ,ZAG1
	DEC	HL
	LD	BC,KTAB
	AND	A	;CY=0
	SBC	HL,BC
	SLA	L
	LD	BC,FTAB
	ADD	HL,BC
	LD	C,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,C
	JP	(HL)

KTAB:	DB	8	;BACKSTEP
	DB	9	;CURSOR RECHTS
	DB	0CH	;CLS
	DB	16H	;LOESCHEN ZEILENREST
	DB	18H	;LOESCHEN ZEILE,CURSOR ANF.Z.
	DB	1AH	;CURSOR NACH OBEN
	DB	14H	;LOESCHEN BILDSCHIRMREST
	DB	82H	;KURSOR EIN
	DB	83H	;KURSOR AUS
	DB	0DH	;CR
	DB	0AH	;LF
	DB	1	;CURSOR LINKS OBEN
	DB	7	;BEEP
	DB	15H	;CURSOR RECHTS
	DB	84H	;ZEICHEN NORMAL
	DB	85H	;ZEICHEN INVERS
	DB	86H	;ZEICHEN INTENSIV
	DB	87H	;ZEICHEN INTENSIV INVERS
LKTAB:	
FTAB:	DW	BSTP
	DW	CURR
	DW	CLS
	DW	DELLN
	DW	INSLN
	DW	ZRET
	DW	CLCU
	DW	CZSET
	DW	PUABL
	DW	CR
	DW	OUTLF
	DW	CHOME
	DW	BEEPA
	DW	CURR
	DW	NORM
	DW	INV
	DW	ND
	DW	ND	
;
;AUSGABE ZEICHEN <> STEUERET ZEICHEN
;
ZAG1:	AND	7FH
	LD	HL,(CURSR)
	PUSH	AF
	LD	A,(MINV)
	LD	B,A
	POP	AF
	OR	B	;INVERS, WENN 80H
	LD	M,A
	INC	HL
	EX	DE,HL
	LD	HL,BWSE+1
	XOR	A
	SBC	HL,DE
	EX	DE,HL
	JR	NZ,ZAG2
ZAG3:	LD	DE,BWSA
	LD	HL,BWSA+SPALT
	LD	BC,BWSE-BWSA+1-SPALT
	LDIR
	PUSH	DE
	POP	HL
	PUSH	HL
	INC	DE
	LD	(HL),' '
	LD	BC,SPALT-1
	LDIR
	LD	HL,(SOIL)
	LD	DE,SPALT
	XOR	A
	SBC	HL,DE
	LD	(SOIL),HL
	POP	HL
ZAG2:	LD	A,(HL)
	LD	(CZSP),A
	LD	(HL),0FFH
	LD	(CURSR),HL
ND:	RET
;
;BACKSTEP
BSTP:	CALL	PUABL
	DEC	HL
	JR	ZAG2
;
;CURSOR RECHTS
CURR:	CALL	PUABL
	INC	HL
	LD	DE,BWSE
	EX	DE,HL
	OR	A
	SBC	HL,DE
	EX	DE,HL
	LD	A,' '
	JP	C,ZAG1
	JR	ZAG2	
;
;BS-LOESCHEN
CLS:	LD	HL,BWSA
	LD	(HL),' ' 
	LD	BC,BWSE-BWSA
	LD	DE,BWSA+1
	LDIR
	LD	A,' '
	LD	(CZSP),A
	JP	CHOME	
;
;WAGENRUECKLAUF
CR:	LD	HL,(CURSR)
	LD	A,SPALT-1
	CPL
	AND	L
	LD	L,A
	JP	SETCU
;
;BEEP-AUSGABE
BEEPA:	LD	BC,0A040H	;BEEP ARG.
	CALL	BEEP
	RET
;
;LINE-FEED
OUTLF:	LD	DE,BWSE
	LD	HL,(CURSR)
	LD	BC,SPALT
	ADD	HL,BC
	EX	DE,HL
	SBC	HL,DE	;ENDE-NEUE
	EX	DE,HL
	JP	NC,SETCU
	CALL	PUABL	;PUFFER AUFBLENDEN
	CALL	ZAG3
	RET
;
;CURSOR-HOME
CHOME:	LD	DE,BWSA
	LD	HL,(CURSR)
	OR	A
	SBC	HL,DE
	RET	Z
	EX	DE,HL
	JP	SETCU
;
;LOESCHEN BS AB CURSOR
CLCU:	LD	BC,(CURSR)
	LD	HL,BWSE
	SBC	HL,BC	;HL:=ANZ.ZEICH.BIS BWS-ENDE
MDFAA:	LD	DE,1
	LD	A,' '
	LD	BC,(CURSR)
MDFB3:	LD	(BC),A
	INC	BC
	SBC	HL,DE
	JP	NZ,MDFB3
MDFBA:	LD	HL,(CURSR)
	LD	(HL),0FFH	;CURSOR
	RET
;
;LOESCHEN ZEILE
DELLN:	LD	HL,(CURSR)
	LD	A,L
	AND	SPALT-1	;A=SPALTENPOS.
	LD	B,A
	LD	A,SPALT-1
	SUB	B
	RET	Z
	LD	B,A	;ANZ.BIS ENDE ZEILE	
	LD	A,' '
	LD	(CZSP),A
DELL1:	INC	HL
	LD	(HL),' '
	DJNZ	DELL1
	RET
;
;INSERT IN ZEILE
INSLN:	CALL	CR
	LD	A,(CZSP)
	LD	(HL),A
	LD	D,H
	LD	E,L
	INC	DE
	LD	BC,SPALT-1
	LDIR
	LD	HL,(CURSR)
	LD	(HL),0FFH
	LD	A,' '
	LD	(CZSP),A
	RET
;
ZRET:	LD	B,SPALT
ZR1:	PUSH	BC
	CALL	BSTP
	POP	BC
	DJNZ	ZR1
	RET
;
CZSET:	LD	A,0FFH
CZS1:	LD	HL,(CURSR)
	LD	(HL),A
	RET
;
PUABL:	LD	A,(CZSP)
	JR	CZS1
;
;
INV:	LD	A,80H
INV1:	LD	(MINV),A
	RET
;
NORM:	XOR	A
	JR	INV1
;	
;MERKE ESCAPE-ANKUENDIGUNG
MESC:	LD	A,C
	LD	(MLZBS),A
	RET
;
;CURSOR-DIREKT-POSITIONIERUNG 
ANKXY:	LD	A,(MSEQU)
	CP	0FFH
	JP	Z,SETXY
	LD	A,0FFH
	LD	(MSEQU),A
	LD	A,C
	AND	5FH
	LD	(MZEIP),A
	RET
;
SETXY:	LD	A,C
	CP	3DH	;ADM3A-TERMINAL
	RET	Z
	CP	59H
	RET	Z
	AND	7FH	
	LD	(MSPAP),A
	LD	C,A
	XOR	A
	LD	(MLZBS),A
	LD	(MSEQU),A
	LD	HL,BWSA
	LD	B,0
	ADD	HL,BC
	LD	A,(MZEIP)
	OR	A	;ZEILE 0?
	JP	Z,SETCU
	LD	C,0
	LD	B,A
	LD	D,0
	LD	E,SPALT	;SPALTENZAHL
ZINC:	ADD	HL,DE
	DJNZ	ZINC
SETCU:	PUSH	HL
	LD	DE,BWSE
	SCF
	SBC	HL,DE
	POP	HL
	JR	C,SETC1
	EX	DE,HL
SETC1:	LD	DE,(CURSR)
	LD	A,(CZSP)
	LD	(DE),A
	LD	A,M	;VERDECKTES ZEICHEN
	LD	(CZSP),A
	LD	M,0FFH	;NEUER CURSOR
	LD	(CURSR),HL
	RET
;
;AUSGABE NEUE ZEILE
NL:	PUSH	BC
	PUSH	AF
	LD	C,0DH
	CALL	COOUT
	LD	C,0AH
	CALL	COOUT
	POP	AF
	POP	BC
	RET
;
;LESEN EINES SEKTORS ZU 128 BYTE
;
READ:	CALL	ADRE
	INIR
	XOR	A
	RET
;
;SCHREIBEN EINES BLOCKS
WRITE:	CALL	ADRE
	OTIR
	XOR	A
	RET
;
;ADRESSRECHNUNG FUER GEWAEHLTEN SEKTOR
ADRE:	LD	HL,(TRACK)
	XOR	A
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,(SEC1)
	ADD	HL,DE
	RR	H
	RR	L
	RR	A
	LD	B,A
	LD	A,(GADDR)
	ADD	A,7
	LD	C,A
	OUT	(C),B
	DEC	C
	OUT	(C),L
	LD	A,H
	AND	3
	LD	B,A
	LD	A,(GADDR)
	OR	B
	LD	C,A
	LD	B,128
	LD	HL,(DMA)
	RET
;
INIT:	LD	A,0C3H
	LD	(0),A
	LD	(5),A
	LD	(0C7FDH),A
	LD	HL,WBOTE
	LD	(1),HL
	LD	HL,0C7FDH
CHTP:	LD	(6),HL
	LD	HL,BOS
	LD	(0C7FEH),HL
	LD	A,(AKTLW)
	LD	(TAPUF),A
	LD	A,0CFH
	OUT	(PIOC),A
	LD	A,0FEH
	OUT	(PIOC),A
	LD	A,0FFH
	OUT	(PIOD),A	;ANFANGSINIT
	XOR	A
	LD	(AKTLW),A
	RET
;
;TASTATURSTATUS
;
CONST:	CALL	CONS1
	OR	A
	RET	Z
	LD	A,0FFH
	RET
;
;EIN ZEICHEN VON TASTATUR (INKEY)
;
CONS1:	CALL	CHIT
	PUSH	HL
	PUSH	DE
	PUSH	BC
	RST	20H
	DB	4
	POP	BC
	POP	DE
	POP	HL
	PUSH	AF
	XOR	A
	LD	(TAPUF),A
	LD	A,(IOBPF)
	LD	(AKTLW),A
	POP	AF
	RET
;
;DRUCKEREINBINDUNG V.24-MODUL RIESA
;
LIST:	LD	A,(EXDT)
	OR	A
	LD	A,C
	JP	NZ,EXDR	;EXTERNER TREIBER
ZDR1:	PUSH	HL
	PUSH	BC
	LD	L,A
	LD	A,0CFH
	OUT	(PIOC),A
	LD	A,0FEH
	OUT	(PIOC),A
PRNT1:	PUSH	HL
	XOR	A
	LD	(TAPUF),A	;BLINDE BREAK-ABFRAGE
	CALL	CONS1
	CP	1BH	;ESCAPE
	SCF
	POP	HL
	JR	Z,PRE
	IN	A,(PIOD)	;DRUCKERSTATUS LESEN
	AND	8	;A3=CTS
	JR	NZ,PRNT1
	LD	B,10	;10 BITS
	LD	H,0FFH	;STOP-BITS
	SLA	L	;STARTBIT EINSCHIEBEN
	RL	H
	DI
	PUSH	DE
	LD	A,(F2_4)	
	CP	4	;4MHz
	JR	Z,F4
	CP	2	;2MHz
	JR	Z,F2
	IN	A,(4)
	BIT	6,A
F2:	LD	D,10	;ZK 2MHz
	JR	Z,SEND
F4:	LD	D,23	;ZK 4MHz
SEND:	LD	A,L
	OUT	(PIOD),A
	SRL	H
	RR	L
	LD	C,D	;ZK
TIME:	DEC	C
	JR	NZ,TIME
	DJNZ	SEND
	EI
	OR	A	;CY=0
	POP	DE
PRE:	POP	BC
	POP	HL
	RET
;
;DRUCKERSTATUS V.24-MODUL RIESA
LSTS:	LD	A,(EXDT)
	OR	A
	LD	A,0FFH
	RET	NZ		;EXTERNER TREIBER
	IN	A,(PIOD)	;DRUCKERSTATUS
	AND	8	;CTS-SIGNAL
	LD	A,0FFH
	RET	Z		;BEREIT
	XOR	A	
	RET
;
PUNCH:	RET
	NOP
	NOP
;
READR:	RET
	NOP
	NOP
;
;LOESCHEN RAM-FLOPPY
;
RFDEL:	LD	A,(GADDR)
	LD	E,A
	ADD	A,6
	LD	C,A
	LD	H,C	;IO-ADR.LATCH
	XOR	A
	LD	D,A	;BLOCKZAEHLER/SEITE
DEL3:	OUT	(C),D	;RESET LATCH
	INC	C
	LD	L,C	;IO-ADR-ZAEHLER
	XOR	A
	OUT	(C),A	;RESET ZAEHLER
	LD	C,E
	LD	B,0
	LD	A,0E5H
DEL2:	OUT	(C),A
	DJNZ	DEL2
	INC	D
	LD	C,H
	JR	NZ,DEL3
	INC	E
	LD	A,(GADDR)
	ADD	A,4
	CP	E
	JR	NC,DEL3
	RET
;
;SAVE DISK 256K
;
SAVED:	CALL	CLS
	CALL	NL
	CALL	NL
	CALL	NL
	LD	HL,0
	LD	(ARG1),HL
	LD	(ARG2),HL
	LD	(ARG3),HL
	CALL	KAUFB	;AUFBEREITUNG KOPFPUFFER
	RET	C
	LD	HL,0E0H
	LD	IX,0
	LD	DE,5000	;ANZ.SYNC-BITS
	CALL	BSMK
	LD	BC,BWSA
	CALL	SDMA	;PUFFERANFANG
	LD	IX,0
	LD	DE,0
RDDSK:	LD	C,E
	CALL	SSEC	;1.SECTOR=0
	LD	C,D
	CALL	STRCK	;1.TRACK=0
	EXX
	CALL	READ
	LD	HL,BWSA
	LD	B,4
	LD	DE,40
RDDS1:	PUSH	BC
	PUSH	IX
	CALL	BSMK
	POP	IX
	POP	BC
	LD	DE,1
	ADD	IX,DE
	LD	DE,14
	DJNZ	RDDS1
	EXX
	CALL	CONS1
	CP	3
	JR	Z,SAVEE
	INC	E	;SEC+1
	LD	A,E
	CP	16	;16 SECTORS
	JR	C,RDDSK
	LD	E,0
	INC	D
	LD	A,D
	RST	20H
	DB	6
	RST	20H
	DB	14
	RST	20H
	DB	14
	LD	A,D
	CP	128	;128 TRACKS
	JR	C,RDDSK
SAVEE:	LD	DE,40	;SYNC-BITS
	LD	IX,0FFFFH
	LD	HL,BWSA
	CALL	BSMK
	RET
;
;LOAD DISK
;
LDDSK:	CALL	CLS
	CALL	NL
	CALL	NL
	CALL	NL
	LD	HL,0EFFFH
	LD	(ARG2),HL
LDDS1:	CALL	SUCHK
	RET	C
	LD	A,(0ECH)
	CP	'D'
	JR	NZ,LDDS1
	LD	HL,0F0H
	LD	B,16
LDDS2:	LD	C,(HL)
	CALL	COOUT	
	INC	HL
	DJNZ	LDDS2
	CALL	NL
	CALL	NL
	LD	HL,0
	LD	(ZILAD),HL
	LD	DE,0
LDDS4:	LD	B,4
	LD	HL,BWSA
LDDS3:	PUSH	DE
	PUSH	BC
	CALL	BLMK
	POP	BC
	POP	DE
	RET	C
	DJNZ	LDDS3
	LD	C,E
	CALL	SSEC
	LD	C,D
	CALL	STRCK
	LD	BC,BWSA
	CALL	SDMA
	PUSH	DE
	CALL	WRITE
	POP	DE
	CALL	CONS1	
	CP	3
	RET	Z
	INC	E
	LD	A,E
	CP	16	;16 SECTOREN
	JR	NZ,LDDS4
	INC	D
	LD	E,0
	LD	A,D
	RST	20H
	DB	6
	RST	20H
	DB	14
	RST	20H
	DB	14
	LD	A,D
	CP	128
	JR	C,LDDS4
LOADE:	RET
;
; UP - KOPFAUFBEREITUNG IM PUFFER
;
KAUFB:	CALL	L1
	EX	DE,HL
	LDIR	;F]LLEN PUFFER ARG1 U. 2
	LD	HL,(ARG3)
	LD	(ARG3P),HL	;ARG3
	LD	HL,PUANF-3H
	LD	A,0D3H	;KOPFKENNZEICHEN -> PUFFER
	LD	B,3
KOKZ:	LD	(HL),A
	INC	HL
	DJNZ	KOKZ	;3*D3H=KZ-NAMEN
	CALL	L3	;EING.TYP U.NAME
	LD	A,'D'
	LD	(TYPP),A
	LD	HL,(SOIL)
	LD	BC,10H
	LD	DE,PUANF
	LDIR	;LADEN PUFFER MIT NAME
	CALL	NL
	RET
;
;SUCHEN NACH KOPFBLOCK
;
SUCHK:	CALL	CONS1	;BREAK?
	CP	3	;^C
	JR	NZ,KBRAK
	CALL	NL
	SCF
	RET
;
KBRAK:	LD	A,15+PUANF 
	LD	(ARG2),A
	LD	HL,PUANF-10H
	CALL	BL	;LADEN KOPFBLOCK
	JR	NZ,SUCHK
	LD	B,3
	LD	HL,PUANF-3
KOKO:	LD	A,(HL)
	CP	0D3H	;KONTROLLE KOPFBLOCK
	INC	HL
	JR	NZ,SUCHK
	DJNZ	KOKO
	RET
;
; UP - LESEN EINES BLOCKS MIT KONTROLLE
; INPUT: (ZILAD) - ZU LESENDER BLOCK
;          HL    - DMA
; OUTPUT: (ZILAD)- NAECHSTER ZU LESENDE BLOCK
; CY=1 WENN BREAK ODER ENDEBLOCK
;
BLMK:	CALL	BL
	JR	Z,DLM3
;
; BLOCKSUCHEN
;
BADR:	LD	BC,39H	;TONKENNWERT
	CALL	BEEP
	CALL	BADRC
SUCHB:	CALL	CONS1
	CP	3
	SCF		;ABBRUCH-KZ
	RET	Z
	LD	BC,20H
	AND	A
	SBC	HL,BC
WIED:	CALL	BL
	JR	NZ,SUCHB
DLM3:	LD	A,D
	AND	E
	INC	A	;ENDEBLOCK?
	SCF
	RET	Z
	PUSH	HL
	LD	HL,(ZILAD)
	EX	DE,HL
	AND	A
	SBC	HL,DE
	POP	HL
	JR	Z,DLM1	;WAR GESUCHTER BLOCK
	JR	NC,BADR	;GELESENER BLOCK ZU GROSS
	CALL	BLZKL	;ZU KLEIN
	JR	SUCHB
DLM1:	INC	DE
	LD	(ZILAD),DE
	OR	A	;CY=0
	RET
;
;ARGUMENTE LADEN
;
L1:	LD	HL,ARG1P
	LD	DE,ARG1
	LD	BC,4
	RET
;
; TYP + NAME LADEN
;
L3:	RST	20H
	DB	2H,' filename',0BAH
	LD	HL,(CURSR)
	LD	(SOIL),HL
	LD	C,0FFH
ZKINP:	INC	C
	CALL	CONIN
	CP	3
	SCF
	RET	Z
	CP	8
	JR	NZ,OUTCH
	DEC	C
	JP	M,ZKINP
ENDE:	DEC	C
OUTCH:	CP	0DH
	RST	20H
	DB	0
	LD	A,C
	LD	(DATA+2),A
	SCF
	CCF
	RET	Z  
	LD	A,10H	;16 ZEICHEN
	CP	C
	LD	A,8
	JR	NZ,ZKINP
	JR	ENDE
;
; FEHLERMELDUNGEN KASSETTENINTERFACE
;
BLZKL:	PUSH	HL
	RST	20H
	DB	2,'>>',0A0H
	JR	BADR1
BADRC:	PUSH	HL
	RST	20H
	DB	2,'<<',0A0H
BADR1:	LD	HL,(CURSR)
	LD	(HL),' '
	DEC	HL
	DEC	HL
	DEC	HL
	LD	(CURSR),HL
	POP	HL
	RET
;
; UP-SCHREIBEN EINES BLOCKS MIT BLOCKADRESSE
;
BSMK:	LD	B,112	;VORBLOCK
BSX1:	DJNZ	BSX1
	CALL	FLOUT
	DEC	DE
	LD	A,E
	OR	D
	JR	NZ,BSMK
	LD	C,2
BSMO:	LD	B,53
BSX5:	DJNZ	BSX5
	CALL	FLOUT
	DEC	C
	JR	NZ,BSMO
	PUSH	IX	;IX=KOPFINHALT
	POP	DE	
	LD	B,18	;BSNO AUSGABE KENNZEICHEN
BSXO:	DJNZ	BSXO
	CALL	WS	;SCHREIBEN EINES DOPPELBYTES
	LD	B,15
BSW1:	DJNZ	BSW1
	LD	C,16	;DATEN 16x16=32x8
BSM1:	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	ADD	IX,DE	;AUFADDIEREN CHECKSUMME
	INC	HL
	PUSH	BC
	CALL	WS
	POP	BC
	DEC	C
	JR	Z,BSM2
	LD	B,14
BSX2:	DJNZ	BSX2
	JR	BSM1
BSM2:	PUSH	IX	;ENDE DATEN
	POP	DE	;AUSGABE CHECKSUMME
	LD	B,16
BSX3:	DJNZ	BSX3
	CALL	WS
	RET
;
; UP - AUSGABE EINES WORTES - 16 BIT AUS DE
;
WS:	LD	C,16
WSMO:	SRL	D
	RR	E
	JR	NC,WSM1
	LD	B,3	;WSN1...
BSWS1:	DJNZ	BSWS1
	NOP
	JR	WSM3
WSM1:	CALL	FLOUT
WSM3:	LD	B,25	;WSN2...
BSWS2:	DJNZ	BSWS2
WSM2:	CALL	FLOUT
	DEC	C
	RET	Z  
	LD	B,21	;WSN3...
BSWS3:	DJNZ	BSWS3
	JR	WSMO
;
; UP - FLANKENAUSGABE BIT 7 PORT B
;
FLOUT:	IN	A,(2)
	XOR	80H
	OUT	(2),A
	RET 
;
; UP ZUR EINGABE EINES DATENBLOCKS
; INPUT:HL=AADR.
;OUTPUT:HL=AADR.NAECHSTER BLOCK
;       DE=(DATA)=KOPFADRESSE
;       Z=1 --> BLOCK FEHLERFREI
;
BL:	CALL	BITIN
	CALL	FIFLA
	LD	C,7
BLM1:	LD	DE,910H	;BLN9...BLN11
	LD	A,7
SW1:	DEC	A
	JR	NZ,SW1
	CALL	BITIN
BLMX:	CALL	BITIN
	JR	NZ,BL	;VERAENDERUNG ERKANNT?
	DEC	D
	JR	NZ,BLMX
	DEC	C
	JR	Z,BLM4	;SYNC.-FELD ERKANNT?
BLM2:	IN	A,(2)
	XOR	B
	BIT	6,A
	JR	NZ,BLM1	;FLANKE ERKANNT?
	DEC	E
	JR	NZ,BLM2	;WARTESCHLEIFE
	JR	BL	;TIME OUT
;
;7 NULLEN ERKANNT
;
BLM4:	CALL	FIFLA
	LD	A,68
SW3:	DEC	A
	JR	NZ,SW3
	CALL	BITIN
	JR	NZ,BLM4	;AUF 1 WARTEN
	CALL	FIFLA
	LD	A,30	;BLN4...
SW4:	DEC	A
	JR	NZ,SW4
	CALL	WL
	LD	(DATA),DE
	PUSH	DE
	POP	IX	;CS-ANFANGSWERT
	LD	A,26	;BLN 10...
	LD	C,16
SW10:	DEC	A
	JR	NZ,SW10
BLM5:	CALL	WL
	ADD	IX,DE
	EX	(SP),IX	;VERET ZOEGERUNG
	EX	(SP),IX
	LD	(HL),E
	INC	HL
	LD	(HL),D
BLM7:	INC	HL
	DEC	C
	JR	Z,BLM8
	LD	A,18	;BLN6...
SW6:	DEC	A
	JR	NZ,SW6
	JR	BLM5
BLM8:	LD	A,18	;BLN7
SW7:	DEC	A
	JR	NZ,SW7
	CALL	WL	;PRUEFSUMME LESEN
	EX	DE,HL
	PUSH	IX
	POP	BC
	SBC	HL,BC
	LD	HL,(DATA)
	EX	DE,HL
	RET
;
; UP - EINLESEN EINES WORTES
;
WL:	PUSH	HL
	LD	L,16
WLMO:	CALL	BITIN
	JR	NZ,WLM1
	XOR	A
	JR	WLM2
WLM1:	SCF
WLM2:	RR	D
	RR	E
	CALL	FIFLA
	DEC	L
	JR	Z,WLM3
	LD	A,30	;WLN1...
SWLN1:	DEC	A
	JR	NZ,SWLN1
	JR	WLMO
WLM3:	POP	HL
	RET
;
; UP - EINGABE EINES BIT
;
BITIN:	IN	A,(2)
	XOR	B
	BIT	6,A
	PUSH	AF
	XOR	B
	LD	B,A
	POP	AF
	RET
;
; UP - FINDEN EINER FLANKE
;
FIFLA:	IN	A,(2)
	XOR	B
	BIT	6,A
	JR	Z,FIFLA
	RET
;
AUTOR:	DB	'(c) by R. Brosig  2/89'
MLZBS:	DB	0	;MERKE LETZTES ZEICH.-BS
MSEQU:	DB	0	;MERKE SEQUENZ
MZEIP:	DB	0	;MERKE ZEILENPOSITION
MSPAP:	DB	0	;MERKE SPALTENPOSITION
	;
DPBA:	DW	0	;NO TRANSLATION TABLE
	DW	0	;6-BYTE ARBEITSZELLEN
	DW	0	;FUER BOS
	DW	0
	DW	DIRBF	;128 BYTE PUFFER
	DW	DPB	;DISK-PARAMETERBLOCK
	DW	CSV	;CHECKSUMMENBEREICH
	DW	ALVA	;BLOCKBELEGUNGSPLAN
	;
DPBB:	DW	0
	DW	0
	DW	0
	DW	0
	DW	DIRBF
	DW	DPB
	DW	CSV
	DW	ALVB
	;
DPB:	DW	16	;SEKTOREN/SPUR
	DB	3	;BLOCKSHIFTFAKTOR
	DB	7	;BLOCKMASKE
	DB	0	;EXTEND-MASKE
	DW	255	;256K KAP.
	DW	63	;MAX.ANZ.DIR.EINTR.-1
	DW	0C0H	;DIREKTORY-BLOECKE
	DW	0	;KEIN DISK CHECK
	DW	0	;KEINE SYSTEMSPUREN
	;
TRACK:	DW	0	;0
SEC1:	DW	0
IOBPF:	DW	0	;PUFFER IO-BYTE
TAPUF:	DW	0
MINV:	DB	0	;MERKE INVERS
GADDR:	DB	98H	;AKTUELLE GRUNDADRESSE
MKOM:	DB	0	;MERKE KOMMANDO
DMA:	DW	80H
CSV:	DB	0
ALVA:	DS	32
ALVB:	DS	32
DIRBF:	DS	128	
	;
	END	;
	;