0rrBrosigS”””BIOS-2RF 2.40   	PN	BIOS-256K-RF
;****************************************
;CPM-BIOS FUER Z1013 UND 2*MP-RAM-FLOPPY
;IG-HC 12/88
;VERS. 2.40
;(c) by Rainer Brosig
;****************************************
;
AKTLW:	EQU	00004H
SOIL:	EQU	00016H	;START OF INPUT LINE
ARG1:	EQU	0001BH
ARG2:	EQU	0001DH
KUZI:	EQU	0001FH
ARG3:	EQU	00023H
CURSR:	EQU	0002BH
CCP:	EQU	0C800H
BOS:	EQU	0D006H
BWSA:	EQU	0EC00H	;BILDW.-SP.-ANF.
BWSE:	EQU	0EFFFH	;ENDE
MON:	EQU	0F000H	;MONITOR-RET
BEEP:	EQU	0FFDCH
DRAKK:	EQU	0FFE8H
NDISK:	EQU	2	;ZWEI LW
STAT:	EQU	0FFF7H
GADDA:	EQU	098H
GADDB:	EQU	058H
CZSP:	EQU	1FH	;CURSOR-ZWISCHEN-SP.
ZILAD:	EQU	25H	;KOPFINHALT LOAD
SPALT:	EQU	64	;BS-SPALTENZAHL
LCS:	EQU	6	;LAENGE CHECKSUMMENBEREICH
CS:	EQU	0F2H	;KONTROLL-CHECKSUMME
ZL:	EQU	SPALT	;ZEILENLAENGE
BSTAB:	EQU	47H
PIOD:	EQU	34H	;ADR.V.24 E/A-MOD.
PIOC:	EQU	35H
MLZT:	EQU	4	;MERKE LETZTES ZEICHEN TAST.
TYPP:	EQU	PUANF-4H	; TYPKENNZEICHENZELLE PUFFER
ARG1P:	EQU	PUANF-10H	; ARG1 PUFFER
ARG2P:	EQU	PUANF-0EH	; ARG2 PUFFER
ARG3P:	EQU	PUANF-0CH	; STARTADRESSE IM PUFFER
PUANF:	EQU	0F0H	; PUFFERANFANG NAME
DATA:	EQU	13H	; KOPFADR. DES LETZTEN BLOCKS

;
	ORG	0DE00H
;
BIOS:	JMP	BOOT
WBOTE:	JMP	WBOOT
	JMP	CONST
	JMP	CONIN
	JMP	COOUT
	JMP	LIST
	JMP	PUNCH
	JMP	READR
	JMP	HOME
	JMP	SDISK
	JMP	STRCK
	JMP	SSEC
	JMP	SDMA
	JMP	READ
	JMP	WRITE
	JMP	LSTS
	JMP	STRAN
	RET
	NOP
	NOP
	RET
	NOP
	NOP
EXDR:	JMP	DRAKK	;EXTERNER DRUCKERTREIBER
MGADA:	DB	98H
MGADB:	DB	58H
EXDT:	DB	0	;SCHALTER, EXT. DR.TR. 0=INTERN
F2_4:	DB	0	;SCHALTER TAKT 0=AUTOANPASSUNG
			;              2=2MHz, 4=4MHz
;
;*********************************************************
;
;KOPF AUF SPUR NULL SETZEN
;
HOME:	LD	BC,0	;SPURNUMMER=0 
	JR	STRCK-#
;
;LAUFWERK AUSWAEHLEN
;INP:C=LW-NUMMER
;OUT:HL=DPH VEKTOR
;
SDISK:	LD	HL,0	;FEHLERCODE
	PUSH	BC
	LD	B,0
	LD	A,C	;C=LW-NUMMER
	CMP	NDISK	;ANZ. LW -1
	JRNC	NVORH-#
DISKS:	LD	(AKTLW),A
	OR	A	;LW A?
	JRNZ	LWB-#	;NICHT A
	LD	A,(MGADA)
	LD	HL,DPBA
	JR	LWEIN-#
LWB:	LD	A,(MGADB)
	LD	HL,DPBB
LWEIN:	LD	(GADDR),A
NVORH:	POP	BC
	RET
;
;SECTORTRANSFORMATION
;INP:BC=LOG.SECTOR
;    DE=ADR.TABELLE
;
STRAN:	LD	B,0
	LD	A,D
	OR 	A
	JRNZ	STRN1-#
	LD	H,B
	LD	L,C
	INC	HL
	RET
;
STRN1:	EX	DE,HL
	ADD	HL,BC
	LD	A,M	;NEUE NUMMER
	LD	(SEC1),A
	LD	L,A
	RET
;
;SEKTOR EINSTELLEN
;INP:C=SEC.NUMMER
;OUT:(SEC1)=C
;
SSEC:	LD	HL,SEC1
	LD	M,C
	INC	HL
	LD	M,0
	RET
;
;SPURWAHL
;INP:C=SPURNUMMER
;OUT:(TRACK)=C
;
STRCK:	LD	HL,TRACK
	LD	M,C
	INC	HL
	LD	M,0
	RET
;
;DMA SETZEN
;INP: BC=DMA
;
SDMA:	LD	(DMA),BC
	RET
;
;KALTSTART
;
MT1:	DB	0CH,0DH,0AH,0AH
	DB	' CP/M 2.2 Z1013 IG-HC',0DH,0AH,0AH
	DB	' mit 2*256K RAM-Floppy V. 2.4',0DH,0AH,0AH,0AH
	DB	'RAM-Floppy loeschen? (K)',0DH,0AH
	DB	'RAM-Floppy laden?    (L) :',0
MT2:	DB	0DH,0AH,'A oder B ? :',0 
MT3:	DB	0DH,0AH,0AH,'weiter mit CP/M? (N)',0
MT4:	DB	0DH,0AH,0AH,'RAM-Floppy laden? (Y)',0
MT5:	DB	0DH,0AH,0AH,'RAM-Floppy saven? (Y)',0
MT6:	DB	0DH,0AH,0AH
	DB	'CCP zerstoert, muss von Kassette'
	DB	' nachgeladen werden!',0
OUT$:	LD	A,(DE)
	OR	A
	RZ	
	LD	C,A
	CALL	COOUT
	INC	DE
	JR	OUT$-#
;
BOOT:	LD	SP,80H
	IN	4
	SET	7,A
	OUT	4	;64*16	
	XOR	A
	LD	(AKTLW),A
	LD	A,8
	LD	(TAPUF),A
	LD	DE,MT1
	CALL	OUT$	
BO1:	CALL	CONIN
	CMP	0DH
	JPZ	KSTRT	
	CMP	3
	JPZ	MON
	RES	5,A
	CMP	'K'
	JRZ	BO2-#
	CMP	'L'
	JRNZ	BO1-#
BO2:	LD	(MKOM),A
	LD	C,A
	CALL	COOUT
	LD	DE,MT2
	CALL	OUT$
BO3:	CALL	CONIN
	RES	5,A
	LD	C,A
	CMP	3
	JRZ	KSTRT-#
	SUB	41H
	JRC	BO3-#
	CALL	COOUT
	LD	C,A
	CALL	SDISK
	LD	A,(MKOM)
	CMP	'K'
	CAZ	RFDEL	;RF-DELETE
	CMP	'L'
	CAZ	LDDSK
KSTRT:	CALL	INIT
	LD	A,8
	LD	(TAPUF),A
	XOR	A
	LD	(TRACK),A
	LD	(SEC1),A
	LD	SP,80H
	JMP	GOCPM
;
WBOOT:	LD	DE,MT3
	CALL	OUT$
	LD	A,8
	LD	(TAPUF),A	;LOESCHEN ZEICHEN
WBO1:	CALL	CONIN
	RES	5,A	;GROSS
	CMP	'N'
	JRZ	SAV1-#
	LD	DE,MT4
	CALL	OUT$
	CALL	CONIN
	RES	5,A
	CMP	'Y'
	CAZ	LDDSK
	JR	GOCPM-#
SAV1:	LD	DE,MT5
	CALL	OUT$
	CALL	CONIN
	RES	5,A
	CMP	'Y'
	CAZ	SAVED
	CALL	CHIT
	IN	4
	RES	7,A
	OUT	4
	JMP	MON
;
GOCPM:	LD	HL,CCP
	XOR	A
	LD	(CCP+7),A	;PUFFER LOESCHEN
	LD	B,LCS	;LAENGE CS-BEREICH
GCPM1:	ADD	M
	INC	HL
	DJNZ	GCPM1-#
	CMP	CS
	JRZ	GCPM2-#
	LD	DE,MT6
	CALL	OUT$
	RST	38H		;KALTSTART
GCPM2:	LD	A,8
	LD	(TAPUF),A
	LD	BC,80H
	CALL	SDMA
	LD	A,(AKTLW)
	CMP	NDISK
	JRC	OK-#
	XOR	A
OK:	LD	C,A
	JMP	CCP
;
;ZEICHEN VON TASTATUR <A>
;
CONIN:	CALL	CHIT
	RST	20H
	DB	1
	PUSH	AF
	CALL	CHTI
	POP	AF
	RET
;
;CHANGE AKTLWE/TAPUF
;
CHIT:	LD	A,(AKTLW)
	LD	(IOBPF),A
	LD	A,(TAPUF)
CHRET:	LD	(AKTLW),A
	RET
;
CHTI:	LD	A,(AKTLW)
	LD	(TAPUF),A
	LD	A,(IOBPF)
	JR	CHRET-#
;
;ZEICHEN AUF BILDSCHIRM <C>
;
COOUT:	PUSH	AF
	PUSH	HL
	PUSH	BC
	PUSH	DE
	CALL	ZAG
	POP	DE
	POP	BC
	POP	HL
	POP	AF
	RET
;
ZAG:	LD	A,(MLZBS)	;MERKE LETZTES ZEICHEN
	CMP	1BH
	JPZ	ANKXY
	LD	A,1BH
	CMP	C
	JPZ	MESC
	LD	A,C
	OR	A
	RZ
	CMP	88H
	JRC	ZAG5-#
	AND	7FH
ZAG5:	LD	HL,KTAB
	LD	BC,LKTAB
	CPIR		;SUCHEN NACH ST.-ZEICHEN
	JRNZ	ZAG1-#
	DEC	HL
	LD	BC,KTAB
	AND	A	;CY=0
	SBC	HL,BC
	SLA	L
	LD	BC,FTAB
	ADD	HL,BC
	LD	C,M
	INC	HL
	LD	H,M
	LD	L,C
	JMP	M

KTAB:	DB	8	;BACKSTEP
	DB	9	;CURSOR RECHTS
	DB	0CH	;CLS
	DB	16H	;LOESCHEN ZEILENREST
	DB	18H	;LOESCHEN ZEILE,CURSOR ANF.Z.
	DB	1AH	;CURSOR NACH OBEN
	DB	14H	;LOESCHEN BILDSCHIRMREST
	DB	82H	;KURSOR EIN
	DB	83H	;KURSOR AUS
	DB	0DH	;CR
	DB	0AH	;LF
	DB	1	;CURSOR LINKS OBEN
	DB	7	;BEEP
	DB	15H	;CURSOR RECHTS
	DB	84H	;ZEICHEN NORMAL
	DB	85H	;ZEICHEN INVERS
	DB	86H	;ZEICHEN INTENSIV
	DB	87H	;ZEICHEN INTENSIV INVERS
LKTAB:	EQU	#-KTAB
FTAB:	DA	BSTP
	DA	CURR
	DA	CLS
	DA	DELLN
	DA	INSLN
	DA	ZRET
	DA	CLCU
	DA	CZSET
	DA	PUABL
	DA	CR
	DA	OUTLF
	DA	CHOME
	DA	BEEPA
	DA	CURR
	DA	NORM
	DA	INV
	DA	ND
	DA	ND	
;
;AUSGABE ZEICHEN <> STEUERZEICHEN
;
ZAG1:	AND	7FH
	LD	HL,(CURSR)
	PUSH	AF
	LD	A,(MINV)
	LD	B,A
	POP	AF
	OR	B	;INVERS, WENN 80H
	LD	M,A
	INC	HL
	EX	DE,HL
	LD	HL,BWSE+1
	XOR	A
	SBC	HL,DE
	EX	DE,HL
	JRNZ	ZAG2-#
ZAG3:	LD	DE,BWSA
	LD	HL,BWSA+SPALT
	LD	BC,BWSE-BWSA+1-SPALT
	LDIR
	PUSH	DE
	POP	HL
	PUSH	HL
	INC	DE
	LD	M,' '
	LD	BC,SPALT-1
	LDIR
	LD	HL,(SOIL)
	LD	DE,SPALT
	XOR	A
	SBC	HL,DE
	LD	(SOIL),HL
	POP	HL
ZAG2:	LD	A,M
	LD	(CZSP),A
	LD	M,0FFH
	LD	(CURSR),HL
ND:	RET
;
;BACKSTEP
BSTP:	CALL	PUABL
	DEC	HL
	JR	ZAG2-#
;
;CURSOR RECHTS
CURR:	CALL	PUABL
	INC	HL
	LD	DE,BWSE
	EX	DE,HL
	OR	A
	SBC	HL,DE
	EX	DE,HL
	LD	A,' '
	JPC	ZAG1
	JR	ZAG2-#	
;
;BS-LOESCHEN
CLS:	LD	HL,BWSA
	LD	M,' ' 
	LD	BC,BWSE-BWSA
	LD	DE,BWSA+1
	LDIR
	LD	A,' '
	LD	(CZSP),A
	JMP	CHOME	
;
;WAGENRUECKLAUF
CR:	LD	HL,(CURSR)
	LD	A,SPALT-1
	CPL
	AND	L
	LD	L,A
	JMP	SETCU
;
;BEEP-AUSGABE
BEEPA:	LD	BC,0A040H	;BEEP ARG.
	CALL	BEEP
	RET
;
;LINE-FEED
OUTLF:	LD	DE,BWSE
	LD	HL,(CURSR)
	LD	BC,SPALT
	ADD	HL,BC
	EX	DE,HL
	SBC	HL,DE	;ENDE-NEUE
	EX	DE,HL
	JPNC	SETCU
	CALL	PUABL	;PUFFER AUFBLENDEN
	CALL	ZAG3
	RET
;
;CURSOR-HOME
CHOME:	LD	DE,BWSA
	LD	HL,(CURSR)
	OR	A
	SBC	HL,DE
	RZ
	EX	DE,HL
	JMP	SETCU
;
;LOESCHEN BS AB CURSOR
CLCU:	LD	BC,(CURSR)
	LD	HL,BWSE
	SBC	HL,BC	;HL:=ANZ.ZEICH.BIS BWS-ENDE
MDFAA:	LD	DE,1
	LD	A,' '
	LD	BC,(CURSR)
MDFB3:	LD	(BC),A
	INC	BC
	SBC	HL,DE
	JPNZ	MDFB3
MDFBA:	LD	HL,(CURSR)
	LD	M,0FFH	;CURSOR
	RET
;
;LOESCHEN ZEILE
DELLN:	LD	HL,(CURSR)
	LD	A,L
	AND	SPALT-1	;A=SPALTENPOS.
	LD	B,A
	LD	A,SPALT-1
	SUB	B
	RZ
	LD	B,A	;ANZ.BIS ENDE ZEILE	
	LD	A,' '
	LD	(CZSP),A
DELL1:	INC	HL
	LD	M,' '
	DJNZ	DELL1-#
	RET
;
;INSERT IN ZEILE
INSLN:	CALL	CR
	LD	A,(CZSP)
	LD	M,A
	LD	D,H
	LD	E,L
	INC	DE
	LD	BC,SPALT-1
	LDIR
	LD	HL,(CURSR)
	LD	M,0FFH
	LD	A,' '
	LD	(CZSP),A
	RET
;
ZRET:	LD	B,SPALT
ZR1:	PUSH	BC
	CALL	BSTP
	POP	BC
	DJNZ	ZR1-#
	RET
;
CZSET:	LD	A,0FFH
CZS1:	LD	HL,(CURSR)
	LD	M,A
	RET
;
PUABL:	LD	A,(CZSP)
	JR	CZS1-#
;
;
INV:	LD	A,80H
INV1:	LD	(MINV),A
	RET
;
NORM:	XOR	A
	JR	INV1-#
;	
;MERKE ESCAPE-ANKUENDIGUNG
MESC:	LD	A,C
	LD	(MLZBS),A
	RET
;
;CURSOR-DIREKT-POSITIONIERUNG 
ANKXY:	LD	A,(MSEQU)
	CMP	0FFH
	JPZ	SETXY
	LD	A,0FFH
	LD	(MSEQU),A
	LD	A,C
	AND	5FH
	LD	(MZEIP),A
	RET
;
SETXY:	LD	A,C
	CMP	3DH	;ADM3A-TERMINAL
	RZ
	CMP	59H
	RZ
	AND	7FH	
	LD	(MSPAP),A
	LD	C,A
	XOR	A
	LD	(MLZBS),A
	LD	(MSEQU),A
	LD	HL,BWSA
	LD	B,0
	ADD	HL,BC
	LD	A,(MZEIP)
	OR	A	;ZEILE 0?
	JPZ	SETCU
	LD	C,0
	LD	B,A
	LD	D,0
	LD	E,SPALT	;SPALTENZAHL
ZINC:	ADD	HL,DE
	DJNZ	ZINC-#
SETCU:	PUSH	HL
	LD	DE,BWSE
	SCF
	SBC	HL,DE
	POP	HL
	JRC	SETC1-#
	EX	DE,HL
SETC1:	LD	DE,(CURSR)
	LD	A,(CZSP)
	LD	(DE),A
	LD	A,M	;VERDECKTES ZEICHEN
	LD	(CZSP),A
	LD	M,0FFH	;NEUER CURSOR
	LD	(CURSR),HL
	RET
;
;AUSGABE NEUE ZEILE
NL:	PUSH	BC
	PUSH	AF
	LD	C,0DH
	CALL	COOUT
	LD	C,0AH
	CALL	COOUT
	POP	AF
	POP	BC
	RET
;
;LESEN EINES SEKTORS ZU 128 BYTE
;
READ:	CALL	ADRE
	INIR
	XOR	A
	RET
;
;SCHREIBEN EINES BLOCKS
WRITE:	CALL	ADRE
	OTIR
	XOR	A
	RET
;
;ADRESSRECHNUNG FUER GEWAEHLTEN SEKTOR
ADRE:	LD	HL,(TRACK)
	XOR	A
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,(SEC1)
	ADD	HL,DE
	RR	H
	RR	L
	RR	A
	LD	B,A
	LD	A,(GADDR)
	ADD	7
	LD	C,A
	OUT	B
	DEC	C
	OUT	L
	LD	A,H
	AND	3
	LD	B,A
	LD	A,(GADDR)
	OR	B
	LD	C,A
	LD	B,128
	LD	HL,(DMA)
	RET
;
INIT:	LD	A,0C3H
	LD	(0),A
	LD	(5),A
	LD	(0C7FDH),A
	LD	HL,WBOTE
	LD	(1),HL
	LD	HL,0C7FDH
CHTP:	LD	(6),HL
	LD	HL,BOS
	LD	(0C7FEH),HL
	LD	A,(AKTLW)
	LD	(TAPUF),A
	LD	A,0CFH
	OUT	PIOC
	LD	A,0FEH
	OUT	PIOC
	LD	A,0FFH
	OUT	PIOD	;ANFANGSINIT
	XOR	A
	LD	(AKTLW),A
	RET
;
;TASTATURSTATUS
;
CONST:	CALL	CONS1
	OR	A
	RZ
	LD	A,0FFH
	RET
;
;EIN ZEICHEN VON TASTATUR (INKEY)
;
CONS1:	CALL	CHIT
	PUSH	HL
	PUSH	DE
	PUSH	BC
	RST	20H
	DB	4
	POP	BC
	POP	DE
	POP	HL
	PUSH	AF
	XOR	A
	LD	(TAPUF),A
	LD	A,(IOBPF)
	LD	(AKTLW),A
	POP	AF
	RET
;
;DRUCKEREINBINDUNG V.24-MODUL RIESA
;
LIST:	LD	A,(EXDT)
	OR	A
	LD	A,C
	JPNZ	EXDR	;EXTERNER TREIBER
ZDR1:	PUSH	HL
	PUSH	BC
	LD	L,A
	LD	A,0CFH
	OUT	PIOC
	LD	A,0FEH
	OUT	PIOC
PRNT1:	PUSH	HL
	XOR	A
	LD	(TAPUF),A	;BLINDE BREAK-ABFRAGE
	CALL	CONS1
	CMP	1BH	;ESCAPE
	SCF
	POP	HL
	JRZ	PRE-#
	IN	PIOD	;DRUCKERSTATUS LESEN
	AND	8	;A3=CTS
	JRNZ	PRNT1-#
	LD	B,10	;10 BITS
	LD	H,0FFH	;STOP-BITS
	SLA	L	;STARTBIT EINSCHIEBEN
	RL	H
	DI
	PUSH	DE
	LD	A,(F2_4)	
	CMP	4	;4MHz
	JRZ	F4-#
	CMP	2	;2MHz
	JRZ	F2-#
	IN	4
	BIT	6,A
F2:	LD	D,10	;ZK 2MHz
	JRZ	SEND-#
F4:	LD	D,23	;ZK 4MHz
SEND:	LD	A,L
	OUT	PIOD
	SRL	H
	RR	L
	LD	C,D	;ZK
TIME:	DEC	C
	JRNZ	TIME-#
	DJNZ	SEND-#
	EI
	OR	A	;CY=0
	POP	DE
PRE:	POP	BC
	POP	HL
	RET
;
;DRUCKERSTATUS V.24-MODUL RIESA
LSTS:	LD	A,(EXDT)
	OR	A
	LD	A,0FFH
	RNZ		;EXTERNER TREIBER
	IN	PIOD	;DRUCKERSTATUS
	AND	8	;CTS-SIGNAL
	LD	A,0FFH
	RZ		;BEREIT
	XOR	A	
	RET
;
PUNCH:	RET
	NOP
	NOP
;
READR:	RET
	NOP
	NOP
;
;LOESCHEN RAM-FLOPPY
;
RFDEL:	LD	A,(GADDR)
	LD	E,A
	ADD	6
	LD	C,A
	LD	H,C	;IO-ADR.LATCH
	XOR	A
	LD	D,A	;BLOCKZAEHLER/SEITE
DEL3:	OUT	D	;RESET LATCH
	INC	C
	LD	L,C	;IO-ADR-ZAEHLER
	XOR	A
	OUT	A	;RESET ZAEHLER
	LD	C,E
	LD	B,0
	LD	A,0E5H
DEL2:	OUT	A
	DJNZ	DEL2-#
	INC	D
	LD	C,H
	JRNZ	DEL3-#
	INC	E
	LD	A,(GADDR)
	ADD	4
	CMP	E
	JRNC	DEL3-#
	RET
;
;SAVE DISK 256K
;
SAVED:	CALL	CLS
	CALL	NL
	CALL	NL
	CALL	NL
	LD	HL,0
	LD	(ARG1),HL
	LD	(ARG2),HL
	LD	(ARG3),HL
	CALL	KAUFB	;AUFBEREITUNG KOPFPUFFER
	RC
	LD	HL,0E0H
	LD	IX,0
	LD	DE,5000	;ANZ.SYNC-BITS
	CALL	BSMK
	LD	BC,BWSA
	CALL	SDMA	;PUFFERANFANG
	LD	IX,0
	LD	DE,0
RDDSK:	LD	C,E
	CALL	SSEC	;1.SECTOR=0
	LD	C,D
	CALL	STRCK	;1.TRACK=0
	EXX
	CALL	READ
	LD	HL,BWSA
	LD	B,4
	LD	DE,40
RDDS1:	PUSH	BC
	PUSH	IX
	CALL	BSMK
	POP	IX
	POP	BC
	LD	DE,1
	ADD	IX,DE
	LD	DE,14
	DJNZ	RDDS1-#
	EXX
	CALL	CONS1
	CMP	3
	JRZ	SAVEE-#
	INC	E	;SEC+1
	LD	A,E
	CMP	16	;16 SECTORS
	JRC	RDDSK-#
	LD	E,0
	INC	D
	LD	A,D
	RST	20H
	DB	6
	RST	20H
	DB	14
	RST	20H
	DB	14
	LD	A,D
	CMP	128	;128 TRACKS
	JRC	RDDSK-#
SAVEE:	LD	DE,40	;SYNC-BITS
	LD	IX,0FFFFH
	LD	HL,BWSA
	CALL	BSMK
	RET
;
;LOAD DISK
;
LDDSK:	CALL	CLS
	CALL	NL
	CALL	NL
	CALL	NL
	LD	HL,0EFFFH
	LD	(ARG2),HL
LDDS1:	CALL	SUCHK
	RC
	LD	A,(0ECH)
	CMP	'D'
	JRNZ	LDDS1-#
	LD	HL,0F0H
	LD	B,16
LDDS2:	LD	C,M
	CALL	COOUT	
	INC	HL
	DJNZ	LDDS2-#
	CALL	NL
	CALL	NL
	LD	HL,0
	LD	(ZILAD),HL
	LD	DE,0
LDDS4:	LD	B,4
	LD	HL,BWSA
LDDS3:	PUSH	DE
	PUSH	BC
	CALL	BLMK
	POP	BC
	POP	DE
	RC
	DJNZ	LDDS3-#
	LD	C,E
	CALL	SSEC
	LD	C,D
	CALL	STRCK
	LD	BC,BWSA
	CALL	SDMA
	PUSH	DE
	CALL	WRITE
	POP	DE
	CALL	CONS1	
	CMP	3
	RZ
	INC	E
	LD	A,E
	CMP	16	;16 SECTOREN
	JRNZ	LDDS4-#
	INC	D
	LD	E,0
	LD	A,D
	RST	20H
	DB	6
	RST	20H
	DB	14
	RST	20H
	DB	14
	LD	A,D
	CMP	128
	JRC	LDDS4-#
LOADE:	RET
;
; UP - KOPFAUFBEREITUNG IM PUFFER
;
KAUFB:	CALL	L1
	EX	DE,HL
	LDIR	;F]LLEN PUFFER ARG1 U. 2
	LD	HL,(ARG3)
	LD	(ARG3P),HL	;ARG3
	LD	HL,PUANF-3H
	LD	A,0D3H	;KOPFKENNZEICHEN -> PUFFER
	LD	B,3
KOKZ:	LD	M,A
	INC	HL
	DJNZ	KOKZ-#	;3*D3H=KZ-NAMEN
	CALL	L3	;EING.TYP U.NAME
	LD	A,'D'
	LD	(TYPP),A
	LD	HL,(SOIL)
	LD	BC,10H
	LD	DE,PUANF
	LDIR	;LADEN PUFFER MIT NAME
	CALL	NL
	RET
;
;SUCHEN NACH KOPFBLOCK
;
SUCHK:	CALL	CONS1	;BREAK?
	CMP	3	;^C
	JRNZ	KBRAK-#
	CALL	NL
	SCF
	RET
;
KBRAK:	LD	A,15+PUANF 
	LD	(ARG2),A
	LD	HL,PUANF-10H
	CALL	BL	;LADEN KOPFBLOCK
	JRNZ	SUCHK-#
	LD	B,3
	LD	HL,PUANF-3
KOKO:	LD	A,M
	CMP	0D3H	;KONTROLLE KOPFBLOCK
	INC	HL
	JRNZ	SUCHK-#
	DJNZ	KOKO-#
	RET
;
; UP - LESEN EINES BLOCKS MIT KONTROLLE
; INPUT: (ZILAD) - ZU LESENDER BLOCK
;          HL    - DMA
; OUTPUT: (ZILAD)- NAECHSTER ZU LESENDE BLOCK
; CY=1 WENN BREAK ODER ENDEBLOCK
;
BLMK:	CALL	BL
	JRZ	DLM3-#
;
; BLOCKSUCHEN
;
BADR:	LD	BC,39H	;TONKENNWERT
	CALL	BEEP
	CALL	BADRC
SUCHB:	CALL	CONS1
	CMP	3
	SCF		;ABBRUCH-KZ
	RZ
	LD	BC,20H
	AND	A
	SBC	HL,BC
WIED:	CALL	BL
	JRNZ	SUCHB-#
DLM3:	LD	A,D
	AND	E
	INC	A	;ENDEBLOCK?
	SCF
	RZ
	PUSH	HL
	LD	HL,(ZILAD)
	EX	DE,HL
	AND	A
	SBC	HL,DE
	POP	HL
	JRZ	DLM1-#	;WAR GESUCHTER BLOCK
	JRNC	BADR-#	;GELESENER BLOCK ZU GROSS
	CALL	BLZKL	;ZU KLEIN
	JR	SUCHB-#
DLM1:	INC	DE
	LD	(ZILAD),DE
	OR	A	;CY=0
	RET
;
;ARGUMENTE LADEN
;
L1:	LD	HL,ARG1P
	LD	DE,ARG1
	LD	BC,4
	RET
;
; TYP + NAME LADEN
;
L3:	RST	20H
	DB	2H,' filename',0BAH
	LD	HL,(CURSR)
	LD	(SOIL),HL
	LD	C,0FFH
ZKINP:	INC	C
	CALL	CONIN
	CMP	3
	SCF
	RZ
	CMP	8
	JRNZ	OUTCH-#
	DEC	C
	JPM	ZKINP
ENDE:	DEC	C
OUTCH:	CMP	0DH
	RST	20H
	DB	0
	LD	A,C
	LD	(DATA+2),A
	SCF
	CCF
	RZ  
	LD	A,10H	;16 ZEICHEN
	CMP	C
	LD	A,8
	JRNZ	ZKINP-#
	JR	ENDE-#
;
; FEHLERMELDUNGEN KASSETTENINTERFACE
;
BLZKL:	PUSH	HL
	RST	20H
	DB	2,'>>',0A0H
	JR	BADR1-#
BADRC:	PUSH	HL
	RST	20H
	DB	2,'<<',0A0H
BADR1:	LD	HL,(CURSR)
	LD	M,' '
	DEC	HL
	DEC	HL
	DEC	HL
	LD	(CURSR),HL
	POP	HL
	RET
;
; UP-SCHREIBEN EINES BLOCKS MIT BLOCKADRESSE
;
BSMK:	LD	B,112	;VORBLOCK
BSX1:	DJNZ	BSX1-#
	CALL	FLOUT
	DEC	DE
	LD	A,E
	OR	D
	JRNZ	BSMK-#
	LD	C,2
BSMO:	LD	B,53
BSX5:	DJNZ	BSX5-#
	CALL	FLOUT
	DEC	C
	JRNZ	BSMO-#
	PUSH	IX	;IX=KOPFINHALT
	POP	DE	
	LD	B,18	;BSNO AUSGABE KENNZEICHEN
BSXO:	DJNZ	BSXO-#
	CALL	WS	;SCHREIBEN EINES DOPPELBYTES
	LD	B,15
BSW1:	DJNZ	BSW1-#
	LD	C,16	;DATEN 16x16=32x8
BSM1:	LD	E,M
	INC	HL
	LD	D,M
	ADD	IX,DE	;AUFADDIEREN CHECKSUMME
	INC	HL
	PUSH	BC
	CALL	WS
	POP	BC
	DEC	C
	JRZ	BSM2-#
	LD	B,14
BSX2:	DJNZ	BSX2-#
	JR	BSM1-#
BSM2:	PUSH	IX	;ENDE DATEN
	POP	DE	;AUSGABE CHECKSUMME
	LD	B,16
BSX3:	DJNZ	BSX3-#
	CALL	WS
	RET
;
; UP - AUSGABE EINES WORTES - 16 BIT AUS DE
;
WS:	LD	C,16
WSMO:	SRL	D
	RR	E
	JRNC	WSM1-#
	LD	B,3	;WSN1...
BSWS1:	DJNZ	BSWS1-#
	NOP
	JR	WSM3-#
WSM1:	CALL	FLOUT
WSM3:	LD	B,25	;WSN2...
BSWS2:	DJNZ	BSWS2-#
WSM2:	CALL	FLOUT
	DEC	C
	RZ  
	LD	B,21	;WSN3...
BSWS3:	DJNZ	BSWS3-#
	JR	WSMO-#
;
; UP - FLANKENAUSGABE BIT 7 PORT B
;
FLOUT:	IN	2
	XOR	80H
	OUT	2
	RET 
;
; UP ZUR EINGABE EINES DATENBLOCKS
; INPUT:HL=AADR.
;OUTPUT:HL=AADR.NAECHSTER BLOCK
;       DE=(DATA)=KOPFADRESSE
;       Z=1 --> BLOCK FEHLERFREI
;
BL:	CALL	BITIN
	CALL	FIFLA
	LD	C,7
BLM1:	LD	DE,910H	;BLN9...BLN11
	LD	A,7
SW1:	DEC	A
	JRNZ	SW1-#
	CALL	BITIN
BLMX:	CALL	BITIN
	JRNZ	BL-#	;VERAENDERUNG ERKANNT?
	DEC	D
	JRNZ	BLMX-#
	DEC	C
	JRZ	BLM4-#	;SYNC.-FELD ERKANNT?
BLM2:	IN	2
	XOR	B
	BIT	6,A
	JRNZ	BLM1-#	;FLANKE ERKANNT?
	DEC	E
	JRNZ	BLM2-#	;WARTESCHLEIFE
	JR	BL-#	;TIME OUT
;
;7 NULLEN ERKANNT
;
BLM4:	CALL	FIFLA
	LD	A,68
SW3:	DEC	A
	JRNZ	SW3-#
	CALL	BITIN
	JRNZ	BLM4-#	;AUF 1 WARTEN
	CALL	FIFLA
	LD	A,30	;BLN4...
SW4:	DEC	A
	JRNZ	SW4-#
	CALL	WL
	LD	(DATA),DE
	PUSH	DE
	POP	IX	;CS-ANFANGSWERT
	LD	A,26	;BLN 10...
	LD	C,16
SW10:	DEC	A
	JRNZ	SW10-#
BLM5:	CALL	WL
	ADD	IX,DE
	EX	(SP),IX	;VERZOEGERUNG
	EX	(SP),IX
	LD	M,E
	INC	HL
	LD	M,D
BLM7:	INC	HL
	DEC	C
	JRZ	BLM8-#
	LD	A,18	;BLN6...
SW6:	DEC	A
	JRNZ	SW6-#
	JR	BLM5-#
BLM8:	LD	A,18	;BLN7
SW7:	DEC	A
	JRNZ	SW7-#
	CALL	WL	;PRUEFSUMME LESEN
	EX	DE,HL
	PUSH	IX
	POP	BC
	SBC	HL,BC
	LD	HL,(DATA)
	EX	DE,HL
	RET
;
; UP - EINLESEN EINES WORTES
;
WL:	PUSH	HL
	LD	L,16
WLMO:	CALL	BITIN
	JRNZ	WLM1-#
	XOR	A
	JR	WLM2-#
WLM1:	SCF
WLM2:	RR	D
	RR	E
	CALL	FIFLA
	DEC	L
	JRZ	WLM3-#
	LD	A,30	;WLN1...
SWLN1:	DEC	A
	JRNZ	SWLN1-#
	JR	WLMO-#
WLM3:	POP	HL
	RET
;
; UP - EINGABE EINES BIT
;
BITIN:	IN	2
	XOR	B
	BIT	6,A
	PUSH	AF
	XOR	B
	LD	B,A
	POP	AF
	RET
;
; UP - FINDEN EINER FLANKE
;
FIFLA:	IN	2
	XOR	B
	BIT	6,A
	JRZ	FIFLA-#
	RET
;
AUTOR:	DB	'(c) by R. Brosig  2/89'
MLZBS:	DB	0	;MERKE LETZTES ZEICH.-BS
MSEQU:	DB	0	;MERKE SEQUENZ
MZEIP:	DB	0	;MERKE ZEILENPOSITION
MSPAP:	DB	0	;MERKE SPALTENPOSITION
;
DPBA:	DA	0	;NO TRANSLATION TABLE
	DA	0	;6-BYTE ARBEITSZELLEN
	DA	0	;FUER BOS
	DA	0
	DA	DIRBF	;128 BYTE PUFFER
	DA	DPB	;DISK-PARAMETERBLOCK
	DA	CSV	;CHECKSUMMENBEREICH
	DA	ALVA	;BLOCKBELEGUNGSPLAN
;
DPBB:	DA	0
	DA	0
	DA	0
	DA	0
	DA	DIRBF
	DA	DPB
	DA	CSV
	DA	ALVB
;
DPB:	DA	16	;SEKTOREN/SPUR
	DB	3	;BLOCKSHIFTFAKTOR
	DB	7	;BLOCKMASKE
	DB	0	;EXTEND-MASKE
	DA	255	;256K KAP.
	DA	63	;MAX.ANZ.DIR.EINTR.-1
	DA	0C0H	;DIREKTORY-BLOECKE
	DA	0	;KEIN DISK CHECK
	DA	0	;KEINE SYSTEMSPUREN
;
TRACK:	DA	0	;0
SEC1:	DA	0
IOBPF:	DA	0	;PUFFER IO-BYTE
TAPUF:	DA	0
MINV:	DB	0	;MERKE INVERS
GADDR:	DB	98H	;AKTUELLE GRUNDADRESSE
MKOM:	DB	0	;MERKE KOMMANDO
DMA:	DA	80H
CSV:	DB	0
ALVA:	BER	32
ALVB:	BER	32
DIRBF:	BER	128	
;
	END	;

ÂÂÂÂÂÂÂÂÂÂÂÂÂ