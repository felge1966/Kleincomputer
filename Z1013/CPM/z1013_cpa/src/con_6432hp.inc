; H. Poppe, 18.11.2016
; BWS 32x32/64x32 auf 4Xh


; --- DEF
;
SOIL	EQU	00016h		;START OF INPUT LINE
CZSP	EQU	0001Fh		;CURSOR-ZWISCHEN-SP.
CURSR	EQU	0002Bh	

BWSA	EQU	0E800h	;BWS-Anfang 64x32
BWSE	EQU	0EFFFh	;BWS-Ende
BWSL	EQU	00800h	;BWS-Laenge

MON	EQU	0F000h	;MONITOR-RET
BEEP	EQU	0FFDCh	
STAT	EQU	0FFF7h	

SPALT	EQU	64	;BS-SPALTENZAHL
ZL	EQU	SPALT	;ZEILENLAENGE


;-------------------------------------------------------------
BWSIO	equ	040h	; I/O-Port BWS-Karte


; --- CODE

;*************************************************************
;
;ZEICHEN VON TASTATUR <A>
;
CONIN:	CALL	CHIT	
	RST	20H	
	DB	1	
	PUSH	AF	
	CALL	CHTI	
	POP	AF	
	RET		
;
;CHANGE AKTLWE/TAPUF
;
CHIT:	LD	A,(DISKA)	
	LD	(IOBPF),A	
	LD	A,(TAPUF)	
CHRET:	LD	(DISKA),A	
	RET		
;
CHTI:	LD	A,(DISKA)	
	LD	(TAPUF),A	
	LD	A,(IOBPF)	
	JR	CHRET	
;
;ZEICHEN AUF BILDSCHIRM <C>
;
COOUT:	PUSH	AF	
	PUSH	HL	
	PUSH	BC	
	PUSH	DE	
	CALL	ZAG	
	POP	DE	
	POP	BC	
	POP	HL	
	POP	AF	
	RET		
;
ZAG:	LD	A,(MLZBS)	;MERKE LETZTES ZEICHEN
	CP	A, 1BH	
	JP	Z,ANKXY	
	LD	A,1Bh	
	CP	A, C	
	JP	Z,MESC	
	LD	A,C	
	OR	A	
	RET	Z	
	CP	A, 88h	
	JR	C,ZAG5	
	AND	A, 7Fh	
ZAG5:	LD	HL,KTAB	
	LD	BC,LKTAB	
	CPIR		;SUCHEN NACH ST.-ZEICHEN
	JR	NZ,ZAG1	
	DEC	HL	
	LD	BC,KTAB	
	AND	A, A	;CY=0
	SBC	HL,BC	
	SLA	L	
	LD	BC,FTAB	
	ADD	HL,BC	
	LD	C,(HL)	
	INC	HL	
	LD	H,(HL)	
	LD	L,C	
	JP	(HL)	
;
KTAB:	DB	8	;BACKSTEP
	DB	9	;CURSOR RECHTS
	DB	0CH	;CLS
	DB	16H	;LOESCHEN ZEILENREST
	DB	18H	;LOESCHEN ZEILE,CURSOR ANF.Z.
	DB	1AH	;CURSOR NACH OBEN
	DB	14H	;LOESCHEN BILDSCHIRMREST
	DB	82H	;KURSOR EIN
	DB	83H	;KURSOR AUS
	DB	0DH	;CR
	DB	0AH	;LF
	DB	1	;CURSOR LINKS OBEN
	DB	7	;BEEP
	DB	15H	;CURSOR RECHTS
	DB	84H	;ZEICHEN NORMAL
	DB	85H	;ZEICHEN INVERS
	DB	86H	;ZEICHEN INTENSIV
	DB	87H	;ZEICHEN INTENSIV INVERS
LKTAB	EQU	18	;Laenge der Tabelle
;
FTAB:	DW	BSTP	
	DW	CURR	
	DW	CLS	
	DW	DELLN	
	DW	INSLN	
	DW	ZRET	
	DW	CLCU	
	DW	CZSET	
	DW	PUABL	
	DW	CR	
	DW	OUTLF	
	DW	CHOME	
	DW	BEEPA	
	DW	CURR	
	DW	NORM	
	DW	INV	
	DW	ND	
	DW	ND	
;
;AUSGABE ZEICHEN <> STEUERZEICHEN
;
ZAG1:	AND	A, 7Fh	
	LD	HL,(CURSR)	
	PUSH	AF	
	LD	A,(MINV)	
	LD	B,A	
	POP	AF	
	OR	B	;INVERS, WENN 80H
	LD	(HL),A	
	INC	HL	
	EX	DE,HL	
	LD	HL,BWSE+1	
	XOR	A	
	SBC	HL,DE	
	EX	DE,HL	
	JR	NZ,ZAG2	
ZAG3:	LD	DE,BWSA	
	LD	HL,BWSA+SPALT	
	LD	BC,BWSE-BWSA+1-SPALT	
	LDIR		
	PUSH	DE	
	POP	HL	
	PUSH	HL	
	INC	DE	
	LD	(HL),' '	
	LD	BC,SPALT-1	
	LDIR		
	LD	HL,(SOIL)	
	LD	DE,SPALT	
	XOR	A	
	SBC	HL,DE	
	LD	(SOIL),HL	
	POP	HL	
ZAG2:	LD	A,(HL)	
	LD	(CZSP),A	
	LD	(HL),0FFH	
	LD	(CURSR),HL	
ND:	RET		
;
;BACKSTEP
BSTP:	CALL	PUABL	
	DEC	HL	
	JR	ZAG2	
;
;CURSOR RECHTS
CURR:	CALL	PUABL	
	INC	HL	
	LD	DE,BWSE	
	EX	DE,HL	
	OR	A	
	SBC	HL,DE	
	EX	DE,HL	
	LD	A,' '	
	JP	C,ZAG1	
	JR	ZAG2	
;
;BS Loeschen und Farbe setzen
BCLS:	LD	c,bwsio+2	
	LD	a,01h	;Farb-RAM ein
	OUT	(c),a	
	LD	b,02h	;Farbe gruen auf schwarz
	CALL	BCL1	
	LD	a,00h	;Farb-RAM aus
	OUT	(c),a	
	LD	b,20h	;Leerzeichen
	CALL	BCL1	
	LD	c,bwsio+1	;
	LD	a,00h	;64 Zeichen
	OUT	(c),a	;
	RET		;
;
BCL1:	LD	hl,BWSA	
	LD	de,BWSL	
BCL2:	LD	(hl),b	
	INC	hl	
	DEC	de	
	LD	a,d	
	OR	e	
	JR	nz,BCL2	
	RET		
;
;BS-LOESCHEN
CLS:	LD	HL,BWSA	
	LD	(HL),' '	
	LD	BC,BWSE-BWSA	
	LD	DE,BWSA+1	
	LDIR		
	LD	A,' '	
	LD	(CZSP),A	
	JP	CHOME	
;
;WAGENRUECKLAUF
CR:	LD	HL,(CURSR)	
	LD	A,SPALT-1	
	CPL		
	AND	A, L	
	LD	L,A	
	JP	SETCU	
;
;BEEP-AUSGABE
BEEPA:	LD	BC,0A040h	;BEEP ARG.
	CALL	BEEP	
	RET		
;
;LINE-FEED
OUTLF:	LD	DE,BWSE	
	LD	HL,(CURSR)	
	LD	BC,SPALT	
	ADD	HL,BC	
	EX	DE,HL	
	SBC	HL,DE	;ENDE-NEUE
	EX	DE,HL	
	JP	NC,SETCU	
	CALL	PUABL	;PUFFER AUFBLENDEN
	CALL	ZAG3	
	RET		
;
;CURSOR-HOME
CHOME:	LD	DE,BWSA	
	LD	HL,(CURSR)	
	OR	A	
	SBC	HL,DE	
	RET	Z	
	EX	DE,HL	
	JP	SETCU	
;
;LOESCHEN BS AB CURSOR
CLCU:	LD	BC,(CURSR)	
	LD	HL,BWSE	
	SBC	HL,BC	;HL:=ANZ.ZEICH.BIS BWS-ENDE
MDFAA:	LD	DE,1	
	LD	A,' '	
	LD	BC,(CURSR)	
MDFB3:	LD	(BC),A	
	INC	BC	
	SBC	HL,DE	
	JP	NZ,MDFB3	
MDFBA:	LD	HL,(CURSR)	
	LD	(HL),0FFH	;CURSOR	
	RET
;
;LOESCHEN ZEILE
DELLN:	LD	HL,(CURSR)	
	LD	A,L	
	AND	A, SPALT-1	;A=SPALTENPOS.
	LD	B,A	
	LD	A,SPALT-1	
	SUB	B	
	RET	Z	
	LD	B,A	;ANZ.BIS ENDE ZEILE
	LD	A,' '	
	LD	(CZSP),A	
DELL1:	INC	HL	
	LD	(HL),' '	
	DJNZ	DELL1	
	RET		
;
;INSERT IN ZEILE
INSLN:	CALL	CR	
	LD	A,(CZSP)	
	LD	(HL),A	
	LD	D,H	
	LD	E,L	
	INC	DE	
	LD	BC,SPALT-1	
	LDIR		
	LD	HL,(CURSR)	
	LD	(HL),0FFH	
	LD	A,' '	
	LD	(CZSP),A	
	RET		
;
ZRET:	LD	B,SPALT	
ZR1:	PUSH	BC	
	CALL	BSTP	
	POP	BC	
	DJNZ	ZR1	
	RET		
;
CZSET:	LD	A,0FFH	
CZS1:	LD	HL,(CURSR)	
	LD	(HL),A	
	RET		
;
PUABL:	LD	A,(CZSP)	
	JR	CZS1	
;
;
INV:	LD	A,80H	
INV1:	LD	(MINV),A	
	RET		
;
NORM:	XOR	A	
	JR	INV1	
;
;MERKE ESCAPE-ANKUENDIGUNG
MESC:	LD	A,C	
	LD	(MLZBS),A	
	RET		
;
;CURSOR-DIREKT-POSITIONIERUNG A
ANKXY:	LD	A,(MSEQU)	
	CP	A, 0FFh	
	JP	Z,SETXY	
	LD	A,0FFh	
	LD	(MSEQU),A	
	LD	A,C	
	AND	A, 5Fh	
	LD	(MZEIP),A	
	RET		
;
SETXY:	LD	A,C	
	CP	A, 3Dh	;ADM3A-TERMINAL
	RET	Z	
	CP	A, 59h	
	RET	Z	
	AND	A, 7Fh	
	LD	(MSPAP),A	
	LD	C,A	
	XOR	A	
	LD	(MLZBS),A	
	LD	(MSEQU),A	
	LD	HL,BWSA	
	LD	B,0	
	ADD	HL,BC	
	LD	A,(MZEIP)	
	OR	A	;ZEILE 0?
	JP	Z,SETCU	
	LD	C,0	
	LD	B,A	
	LD	D,0	
	LD	E,SPALT	;SPALTENZAHL
ZINC:	ADD	HL,DE	
	DJNZ	ZINC	
SETCU:	PUSH	HL	
	LD	DE,BWSE	
	SCF		
	SBC	HL,DE	
	POP	HL	
	JR	C,SETC1	
	EX	DE,HL	
SETC1:	LD	DE,(CURSR)	
	LD	A,(CZSP)	
	LD	(DE),A	
	LD	A,(HL)	;VERDECKTES ZEICHEN
	LD	(CZSP),A	
	LD	(HL),0FFh	;NEUER CURSOR
	LD	(CURSR),HL	
	RET		
;
;AUSGABE NEUE ZEILE
NL:	PUSH	BC	
	PUSH	AF	
	LD	C,0Dh	
	CALL	COOUT	
	LD	C,0Ah	
	CALL	COOUT	
	POP	AF	
	POP	BC	
	RET		
;
;TASTATURSTATUS
;
CONST:	CALL	CONS1	
	OR	A	
	RET	Z	
	LD	A,0FFH	
	RET		
;
;EIN ZEICHEN VON TASTATUR (INKEY)
;
CONS1:	CALL	CHIT	
	PUSH	HL	
	PUSH	DE	
	PUSH	BC	
	RST	20H	
	DB	4	
	POP	BC	
	POP	DE	
	POP	HL	
	PUSH	AF	
	XOR	A	
	LD	(TAPUF),A	
	LD	A,(IOBPF)	
	LD	(DISKA),A	
	POP	AF	
	RET		

; --- RAM

MLZBS:	DB	0	;MERKE LETZTES ZEICH.-BS
MSEQU:	DB	0	;MERKE SEQUENZ
MZEIP:	DB	0	;MERKE ZEILENPOSITION
MSPAP:	DB	0	;MERKE SPALTENPOSITION

IOBPF:	DW	0	;PUFFER IO-BYTE
TAPUF:	DW	0	
MINV:	DB	0	;MERKE INVERS

; --- INIT

INIT_CRT:	
	IN	A,(4)	
	SET	7,A	
	OUT	(4),A	;64*16 - Peters Platine
;
	CALL	bcls	;BWS 64x32 loeschen und Farbe
;			;Gruen auf Schwarz setzen


	LD	A,8	
	LD	(TAPUF),A	;LOESCHEN ZEICHEN

	ld	c,0ch
	call	COOUT

	RET
