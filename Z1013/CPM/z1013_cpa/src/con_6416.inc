; aus CP/A VLE 1.1 "ROM-Version 64 ZEICHEN"

; --- DEF
;
SOIL:	EQU	00016H	;START OF INPUT LINE
CZSP:	EQU	0001FH	;CURSOR-ZWISCHEN-SP.
CURSR:	EQU	0002BH	

BWSA:	EQU	0EC00H	;BILDW.-SP.-ANF.
BWSE:	EQU	0EFFFH	;ENDE

MON:	EQU	0F000H	;MONITOR-RET
BOS:	EQU	0D006H	
BEEP:	EQU	0FFDCH	

SPALT:	EQU	64	;BS-SPALTENZAHL
ZL:	EQU	SPALT	;ZEILENLAENGE
;

; --- CODE

;
;*****************************************
;
OUTS:	LD	A,(DE)	
	OR	A	
	RET	Z	
	LD	C,A	
	CALL	COOUT	
	INC	DE	
	JR	OUTS	
;
;*****************************************
;ZEICHEN VON TASTATUR <A>
;*****************************************
;
CONIN:	CALL	CHIT	
	RST	20H	
	DB	1	;ZEICHENEINGABE
	PUSH	AF	
	CALL	CHTI	
	POP	AF	
	RET		
;
;*****************************************
;ZEICHEN AUF BILDSCHIRM <C>
;*****************************************
;
COOUT:	PUSH	AF	
	PUSH	HL	
	PUSH	BC	
	PUSH	DE	
	CALL	ZAG	
	POP	DE	
	POP	BC	
	POP	HL	
	POP	AF	
	RET		
;
ZAG:	LD	A,(MLZBS)	;MERKE LETZTES ZEICHEN
	CP	A, 1BH	
	JP	Z, ANKXY	
	LD	A,1BH	
	CP	A, C	
	JP	Z, MESC	
	LD	A,C	
	OR	A	
	RET	Z	
	LD	HL,KTAB	
	LD	BC,LKTAB	
	CPIR		
	JR	NZ, ZAG1	
	DEC	HL	
	LD	BC,KTAB	
	SBC	HL,BC	
	SLA	L	
	LD	BC,FTAB	
	ADD	HL,BC	
	LD	C,(HL)	
	INC	HL	
	LD	H,(HL)	
	LD	L,C	
	JP	(HL)	

KTAB:	DB	8	;BACKSTEP
	DB	9	;CURSOR RECHTS
	DB	0CH	;CLS
	DB	16H	;LOESCHEN ZEILENREST
	DB	18H	;LOESCHEN ZEILE,CURSOR ANF.Z.
	DB	1AH	;CURSOR NACH OBEN
	DB	14H	;LOESCHEN BILDSCHIRMREST
	DB	82H	;KURSOR EIN
	DB	83H	;KURSOR AUS
	DB	0DH	;CR
	DB	0AH	;LF
	DB	1	;CURSOR LINKS OBEN
	DB	7	;BEEP
	DB	15H	;CURSOR RECHTS
	DB	84H	;ZEICHEN NORMAL
	DB	85H	;ZEICHEN INVERS
	DB	86H	;ZEICHEN INTENSIV
	DB	87H	;ZEICHEN INTENSIV INVERS
LKTAB:	EQU	$-KTAB	
FTAB:	DW	BSTP	
	DW	CURR	
	DW	CLS	
	DW	DELLN	
	DW	INSLN	
	DW	ZRET	
	DW	CLCU	
	DW	CZSET	
	DW	PUABL	
	DW	CR	
	DW	OUTLF	
	DW	CHOME	
	DW	BEEPA	
	DW	CURR	
	DW	NORM	
	DW	INV	
	DW	ND	
	DW	ND	
;AUSGABE ZEICHEN <> STEUERZEICHEN
ZAG1:	AND	A, 7FH	
	LD	HL,(CURSR)	
	PUSH	AF	
	LD	A,(MINV)	
	LD	B,A	
	POP	AF	
	OR	B	;INVERS, WENN 80H
	LD	(HL),A	
	INC	HL	
	EX	DE,HL	
	LD	HL,BWSE+1	
	XOR	A	
	SBC	HL,DE	
	EX	DE,HL	
	JR	NZ, ZAG2	
ZAG3:	LD	DE,BWSA	
	LD	HL,BWSA+SPALT	
	LD	BC,BWSE-BWSA+1-SPALT	
	LDIR		
	PUSH	DE	
	POP	HL	
	PUSH	HL	
	INC	DE	
	LD	(HL),' '	
	LD	BC,SPALT-1	
	LDIR		
	LD	HL,(SOIL)	
	LD	DE,SPALT	
	XOR	A	
	SBC	HL,DE	
	LD	(SOIL),HL	
	POP	HL	
ZAG2:	LD	A,(HL)	
	LD	(CZSP),A	
	LD	(HL),0FFH	
	LD	(CURSR),HL	
ND:	RET		

;BACKSTEP
BSTP:	CALL	PUABL	
	DEC	HL	
	JR	ZAG2	
;CURSOR RECHTS
CURR:	CALL	PUABL	
	INC	HL	
	LD	DE,BWSE	
	EX	DE,HL	
	OR	A	
	SBC	HL,DE	
	EX	DE,HL	
	LD	A,' '	
	JP	C, ZAG1	
	JR	ZAG2	
;BS-LOESCHEN
CLS:	LD	HL,BWSA	
	LD	(HL),' '	
	LD	BC,BWSE-BWSA	
	LD	DE,BWSA+1	
	LDIR		
	LD	A,' '	
	LD	(CZSP),A	
	JP	CHOME	
;
CR:	LD	HL,(CURSR)	
	LD	A,SPALT-1	
	CPL		
	AND	A, L	
	LD	L,A	
	JP	SETCU	
;
BEEPA:	EQU	$	
	CALL	BEEP	
	RET		
;
OUTLF:	LD	DE,BWSE	
	LD	HL,(CURSR)	
	LD	BC,SPALT	
	ADD	HL,BC	
	EX	DE,HL	
	SBC	HL,DE	;ENDE-NEUE
	EX	DE,HL	
	JP	NC, SETCU	
	CALL	PUABL	;PUFFER AUFBLENDEN
	CALL	ZAG3	
	RET		
;
CHOME:	LD	DE,BWSA	
	LD	HL,(CURSR)	
	OR	A	
	SBC	HL,DE	
	RET	Z	
	EX	DE,HL	
	JP	SETCU	
;
;LOESCHEN BS AB CURSOR
;
CLCU:	LD	BC,(CURSR)	
	LD	HL,BWSE	
	SBC	HL,BC	;HL:=ANZ.ZEICH.BIS BWS-ENDE
MDFAA:	LD	DE,1	
	LD	A,' '	
	LD	BC,(CURSR)	
MDFB3:	LD	(BC),A	
	INC	BC	
	SBC	HL,DE	
	JP	NZ, MDFB3	
MDFBA:	LD	HL,(CURSR)	
	LD	(HL),0FFH	;CURSOR
	RET		
;
DELLN:	LD	HL,(CURSR)	
	LD	A,L	
	AND	A, SPALT-1	;A=SPALTENPOS.
	LD	B,A	
	LD	A,SPALT-1	
	SUB	B	
	RET	Z	
	LD	B,A	;ANZ.BIS ENDE ZEILE
	LD	A,' '	
	LD	(CZSP),A	
DELL1:	INC	HL	
	LD	(HL),' '	
	DJNZ	DELL1	
	RET		
;
INSLN:	CALL	CR	
	LD	A,(CZSP)	
	LD	(HL),A	
	LD	D,H	
	LD	E,L	
	INC	DE	
	LD	BC,SPALT-1	
	LDIR		
	LD	HL,(CURSR)	
	LD	(HL),0FFH	
	LD	A,' '	
	LD	(CZSP),A	
	RET		
;
ZRET:	LD	B,SPALT	
ZR1:	PUSH	BC	
	CALL	BSTP	
	POP	BC	
	DJNZ	ZR1	
	RET		
;
CZSET:	LD	A,0FFH	
CZS1:	LD	HL,(CURSR)	
	LD	(HL),A	
	RET		
;
PUABL:	LD	A,(CZSP)	
	JR	CZS1	
;
;
INV:	LD	A,80H	
INV1:	LD	(MINV),A	
	RET		
;
NORM:	XOR	A	
	JR	INV1	
;	
MESC:	LD	A,C	
	LD	(MLZBS),A	
	RET		
;
ANKXY:	LD	A,(MSEQU)	
	CP	A, 0FFH	
	JP	Z, SETXY	
	LD	A,0FFH	
	LD	(MSEQU),A	
	LD	A,C	
	AND	A, 5FH	
	LD	(MZEIP),A	
	RET		
;
SETXY:	LD	A,C	
	CP	A, 3DH	;ADM3A-TERMINAL
	RET	Z	
	CP	A, 59H	
	RET	Z	
	AND	A, 7FH	
	LD	(MSPAP),A	
	LD	C,A	
	XOR	A	
	LD	(MLZBS),A	
	LD	(MSEQU),A	
	LD	HL,BWSA	
	LD	B,0	
	ADD	HL,BC	
	LD	A,(MZEIP)	
	OR	A	;ZEILE 0?
	JP	Z, SETCU	
	LD	C,0	
	LD	B,A	
	LD	D,0	
	LD	E,SPALT	;SPALTENZAHL
ZINC:	ADD	HL,DE	
	DJNZ	ZINC	
SETCU:	PUSH	HL	
	LD	DE,BWSE	
	SCF		
	SBC	HL,DE	
	POP	HL	
	JR	C, SETC1	
	EX	DE,HL	
SETC1:	LD	DE,(CURSR)	
	LD	A,(CZSP)	
	LD	(DE),A	
	LD	A,(HL)	;VERDECKTES ZEICHEN
	LD	(CZSP),A	
	LD	(HL),0FFH	;NEUER CURSOR
	LD	(CURSR),HL	
	RET		
;
;
;
CONST:	CALL	CONS1	
	OR	A	
	RET	Z	
	LD	A,0FFH	
	RET		
;
CONS1:	CALL	CHIT	
	PUSH	HL	
	PUSH	DE	
	PUSH	BC	
	RST	20H	
	DB	4	
	POP	BC	
	POP	DE	
	POP	HL	
	PUSH	AF	
	XOR	A	
	LD	(TAPUF),A	
	LD	A,(IOBPF)	
	LD	(DISKA),A	
	POP	AF	
	RET		

;*****************************************
;
CHIT:	LD	A,(DISKA)	
	LD	(IOBPF),A	
	LD	A,(TAPUF)	
CHRET:	LD	(DISKA),A	
	RET		
;
CHTI:	LD	A,(DISKA)	
	LD	(TAPUF),A	
	LD	A,(IOBPF)	
	JR	CHRET	


; --- RAM


;
;*****************************************
;
MLZBS:	DB	0	;MERKE LETZTES ZEICH.-BS
MSEQU:	DB	0	;MERKE SEQUENZ
MZEIP:	DB	0	;MERKE ZEILENPOSITION
MSPAP:	DB	0	;MERKE SPALTENPOSITION
;
;
;*****************************************
;
;
IOBPF:	DW	0	;PUFFER I/O BYTE
TAPUF:	DW	0	
MINV:	DB	0	


; --- INIT

INIT_CRT:	
	IN	A, 4	;64-Zeichen-BWS
	AND	A, 0F0H	
	SET	7,A	
	OUT	4, A	

	LD	A,(4)	
	LD	(TAPUF),A	

	ld	c,0ch
	call	COOUT

	RET		
